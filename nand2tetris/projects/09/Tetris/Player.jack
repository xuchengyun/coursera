/**
 * represents score, time, and level that the player earned
 * (TODO: preserve previous status for back-to-back/combo bonus)
 */
class Player {
  field boolean back_to_back;

  // consider having lives
  field int score;
  field int next_score;
  field int level;

  constructor Player new() {
    let score = 0;
    let next_score = 4800;
    let level = 1;
    let back_to_back = false;

    return this;
  }

  method void dispose() {
    do Memory.deAlloc(this);
  }

  method void updateLevel() {
    if (score < next_score) {
      return;
    }

    while (~(score < next_score)) {
      let next_score = next_score + 4800; // TODO: reconsider proper level gaps
      let level = level + 1;
    }
    do ScoreBoard.printLevel(level);
    return;
  }

  method int getLevel() {
    return level;
  }

  method void addScoreForLineClear(int line_count, boolean tSpin, boolean tSpinMini) {
    var int base;
    var boolean last_b2b;

    if (~(line_count > 0) & ~tSpin) {
      return;
    }

    let last_b2b = back_to_back;

    if (tSpin) {
      if (tSpinMini) {
        if (line_count = 0) {
          let base = 100;
          let back_to_back = false;
        }
        if (line_count = 1) {
          if (back_to_back) {
            let base = 300;
          } else {
            let base = 200;
          }
          let back_to_back = true;
        }
        if (line_count = 2) {
          if (back_to_back) {
            let base = 600;
          } else {
            let base = 400;
          }
          let back_to_back = true;
        }
      } else {
        if (line_count = 0) {
          let base = 400;
          let back_to_back = false;
        }
        if (line_count = 1) {
          if (back_to_back) {
            let base = 1200;
          } else {
            let base = 800;
          }
          let back_to_back = true;
        }
        if (line_count = 2) {
          if (back_to_back) {
            let base = 1800;
          } else {
            let base = 1200;
          }
          let back_to_back = true;
        }
        if (line_count = 3) {
          if (back_to_back) {
            let base = 2400;
          } else {
            let base = 1600;
          }
          let back_to_back = true;
        }
      }
    } else {
      if (line_count = 1) {
        let base = 100;
        let back_to_back = false;
      }
      if (line_count = 2) {
        let base = 300;
        let back_to_back = false;
      }
      if (line_count = 3) {
        let base = 500;
        let back_to_back = false;
      }
      if (line_count = 4) {
        if (back_to_back) {
          let base = 1200;
        } else {
          let base = 800;
        }
        let back_to_back = true;
      }
    }

    do addScore(base * level);

    do ScoreBoard.printLineClear(line_count, tSpin, tSpinMini, last_b2b & back_to_back);

    return;
  }

  method void addScoreForSoftDrop() {
    do addScore(1);
    return;
  }

  method void addScoreForHardDrop(int lines) {
    if (lines > 0) {
      do addScore(2 * lines);
    }
    return;
  }

  method void addScore(int diff) {
    let score = score + diff;
    do ScoreBoard.printScore(score);

    do updateLevel();
    return;
  }
}
