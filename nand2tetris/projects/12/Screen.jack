// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Screen.jack

/**
 * A library of functions for displaying graphics on the screen.
 * The Hack physical screen consists of 256 rows (indexed 0..255, top to bottom)
 * of 512 pixels each (indexed 0..511, left to right). The top left pixel on 
 * the screen is indexed (0,0).
 */
class Screen {
    static boolean color;

    /** Initializes the Screen. */
    function void init() {
        let color = true;
        return;
    }

    /** Erases the entire screen. */
    function void clearScreen() {
        return;
    }

    /** Sets the current color, to be used for all subsequent drawXXX commands.
     *  Black is represented by true, white by false. */
    function void setColor(boolean b) {
        let color = b;
        return;
    }

    /** Draws the (x,y) pixel, using the current color. */
    function void drawPixel(int x, int y) {
        var int addr, value, mask;
        let addr = (32 * y) + (x / 16);
        let value = Memory.peek(16384 + addr);
        let mask = Math.twoToThe(x & 15);
        if (color) {
            let value = value | mask;
        } else {
            let value = value & (~mask);
        }
        do Memory.poke(16384 + addr, value);
        return;
    }

    /** Draws a line from pixel (x1,y1) to pixel (x2,y2), using the current color. */
    function void drawLine(int x1, int y1, int x2, int y2) {
        var int a, b, dx, dy, diff;
        let a = 0;
        let b = 0;
        let diff = 0;
        let dx = x2 - x1;
        let dy = y2 - y1;
        if (dx < 0) {
            do Screen.drawLine(x2, y2, x1, y1);
            return;
        }
        if (dx = 0) {
            do Screen.drawVerticalLine(x1, y1, y2);
            return;
        }

        if (dy = 0) {
            do Screen.drawHorizontalLine(x1, x2, y1);
            return;
        }

        while((~(a > dx)) & (~(b > Math.abs(dy)))) {
            if (dy < 0) {
                do Screen.drawPixel(x1 + a, y1 - b);
            } else {
                do Screen.drawPixel(x1 + a, y1 + b);
            }
            if (diff < 0) {
                let a = a + 1;
                let diff = diff + Math.abs(dy);
            } else {
                let b = b + 1;
                let diff = diff - dx;
            }
        }
        return;
    }

    /** Draws a filled rectangle whose top left corner is (x1, y1)
     * and bottom right corner is (x2,y2), using the current color. */
    function void drawRectangle(int x1, int y1, int x2, int y2) {
        while (~(x1 > x2)) {
            do Screen.drawLine(x1, y1, x1, y2);
            let x1 = x1 + 1;
        }
        return;
    }

    /** Draws a filled circle of radius r<=181 around (x,y), using the current color. */
    function void drawCircle(int x, int y, int r) {
        var int dy, rSquare, tmp;
        let dy = -r;
        let rSquare = r * r;
        while (~(dy > r)) {
            let tmp = Math.sqrt(rSquare - (dy * dy));
            do Screen.drawLine(x - tmp, y + dy, x + tmp, y+ dy);
            let dy = dy + 1;
        }
        return;
    }

    function void drawVerticalLine(int x, int y1, int y2) {
        if (y1 > y2) {
            do Screen.drawVerticalLine(x, y2, y1);
            return;
        }
        while (~(y1 > y2)) {
            do Screen.drawPixel(x, y1);
            let y1 = y1 + 1;
        }
        return;
    }

    function void drawHorizontalLine(int x1, int x2, int y) {
        if (x1 > x2) {
            do Screen.drawHorizontalLine(x2, x1, y);
            return;
        }
        while (~(x1 > x2)) {
            do Screen.drawPixel(x1, y);
            let x1 = x1 + 1;
        }
        return;
    }

}
